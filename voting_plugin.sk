#
#	A voting plugin written by HenryRenYz
#
#	Skript v2.4
#
#	Dependencies: 
#		Script (Must have)
#		Skungee (Install this if you want to vote BungeeCord command)
#		SkRayFall (Bossbar effect)
#		SkUtilities (For run command as operator)
#	Command Tag variable instructions({ctag}):
#		{ctag} = 1 run command as console
#		{ctag} = 2 run command as BungeeCord console
#		{ctag} = 3 run command as operator
#		{ctag} = 4 run command as player
#		{ctag} = 5 run command as BungeeCord proxy
#	!!!You have to remove the unwanted functions COMPLETELY to make everything works.
#	!!!你需要完全删除与不需要功能相关的代码才能使这个脚本发挥作用。
options:
    version: 1.2  #Do NOT touch this
    language: zh_CN
on load:
	#initialization
	delete {isvoting}
	delete {timer}
	delete {support}
	delete {voted::*}
	delete {name}
	delete {command}
	delete {welltotal}
	send "§3§lVoting Plugin §freloaded!" to console
	set {cancel} to 1
	wait 5 seconds
	delete {cancel}
command /voting <text> [<text>] [<text>] [<text>]:
	usage: "§8» §c用法： /vote <create/list/history/yes/no>"
	aliases: /vot,/vote
	trigger:
		#vote agree
		if arg 1 is "yes":
			if {name} is not set:
				message "§8» §c没有正在进行的投票！"
			else:
				if {voted::%player%} is 1:
					message "§8» §c你已经投过票了！"
				if {voted::%player%} is not 1:
					message "§8» §7你给§e%{name}%§7投了支持票！"
					add 1 to {support}
					broadcast "§8» §a投票§6%{name}%§7 (§a§l%{support}%§7/%{welltotal}%)"
					set {voted::%player%} to 1
		#vote disagree
		if arg 1 is "no":
			if {name} is not set:
				message "§8» §c没有正在进行的投票！"
			else:
				if {voted::%player%} is 1:
					message "§8» §7已删除你给§e%{name}%§7的支持票！"
					remove 1 from {support}
					broadcast "§8» §a投票§6%{name}%§7 (§a§l%{support}%§7/%{welltotal}%)"
				if {voted::%player%} is not 1:
					message "§8» §c你还没有投过支持票！"
		#create vote
		if arg 1 is "create":
			if arg 2 is not set:
				message "§8» §c用法： /vote create <名称> [/restart /kick /removedrops]"
			else:
				if {isvoting} is not set:
					if {timer} is not set:
						if number of all players is greater than 3:
							if arg 3 is not set:
								set {name} to arg 2
								set {support} to 0
								set {isvoting} to 1
								send "§8» §e%player%§a发起了投票§6%{name}%" to all players
							else:
								#example 1 - vote to restart the server
								if arg 3 is "restart":
									set {name} to arg 2
									set {support} to 0
									set {isvoting} to 1
									set {command} to "/serverrestart"
									set {ctag} to 1
									send "§8» §e%player%§a发起了投票§6%{name}% §7(重启)" to all players
								#example 2 - vote to kick sb. from bungeecord server
								if arg 3 is "kick":
									set {name} to arg 2
									set {support} to 0
									set {isvoting} to 1
									set {command} to "/kick %arg 4% §e玩家投票决定将您请出服务器"
									set {ctag} to 2
									send "§8» §e%player%§a发起了投票§6%{name}% §7(踢出%arg 4%)" to all players
								#example 3 - vote to remove all dropped items by executing command as a player
								if arg 3 is "removedrops":
									set {name} to arg 2
									set {support} to 0
									set {isvoting} to 1
									set {command} to "/removedrops"
									set {ctag} to 3
									send "§8» §e%player%§a发起了投票§6%{name}% §7(清除掉落物)" to all players
						else:
							message "§8» §c在线人数不足以开启投票！"
					else:
						message "§8» §c请等待§e§l%{timer}%§c分钟之后才能再次发起投票"
				else:
					message "§8» §c已经有投票发起了！"
		#create admin vote
		if arg 1 is "admincreate":
			if event-player has permission "player.op":
				if arg 2 is not set:
					message "§8» §c用法： /vote admincreate <名称> [bungee] [/command]"
				else:
					if {isvoting} is not set:
						if arg 3 is not set:
							set {name} to arg 2
							set {support} to 0
							set {isvoting} to 1
							send "§8» §e%player%§a发起了投票§6%{name}%" to all players
						else:
							if arg 3 is "bungee":
								set {name} to arg 2
								set {support} to 0
								set {isvoting} to 1
								set {command} to "%arg 4%"
								set {ctag} to 2
								send "§8» §e%player%§a发起了投票§6%{name}% §7(%{command}%)" to all players
							else:
								set {name} to arg 2
								set {support} to 0
								set {isvoting} to 1
								set {command} to "%arg 3% %arg 4%"
								set {ctag} to 1
								send "§8» §e%player%§a发起了投票§6%{name}% §7(%{command}%)" to all players
					else:
						message "§8» §c已经有投票发起了！"
			else:
				message "§8» §c你不能这么做！"
		#countdown system
		if {isvoting} is 1:
			if {timer} is not set:
				set {timer} to 30
				create bossbar titled "1" and id "voting" for all players with progress 100 with colors red
				loop 100 times:
					set {welltotal} to round up (0.5 * number of all players)
					set bossbar "voting" value to (100 - loop-number)
					set bossbar "voting" title to "§6投票 §e%arg 2% §7 §a§l%{support}%§7/%{welltotal}%"
					if {support} is {welltotal}:
						remove bossbar "voting"
						broadcast "§8» §a投票通过！ 结果： §6§l%{support}% 同意 §7/ %number of all players% 玩家"
						if {ctag} is 1:
							execute console command "%{command}%"
						if {ctag} is 2:
							execute bungee console command "%{command}%"
						if {ctag} is 3:
							make player run command "%{command}%" as op
						if {ctag} is 4:
							execute player command "%{command}%"
						if {ctag} is 5:
							execute bungee proxy command "%{command}%"
						exit loop
					if {cancel} is 1:
						stop loop
					wait 0.3 second
				remove bossbar "voting"
				set {_now} to now formatted as "yyyy.MM.dd HH:mm z"
				if {support} is {welltotal}:
					add "§6%{_now}% §7- §e%player% §7- §6%{name}%§7（%{command}%） §7- §2通过" to {history::*}
					delete {isvoting}
					delete {command}
					delete {name}
					delete {ctag}
					delete {voted::*}
					delete {support}
					loop 1800 times:
						wait 1 second
						remove (1 / 60) from {timer}
						if {cancel} is 1:
							stop loop
				else:
					add "§6%{_now}% §7- §e%player% §7- §6%{name}%§7（%{command}%） §7- §c未通过" to {history::*}
					broadcast "§8» §c投票未通过！"
					delete {isvoting}
					delete {command}
					delete {name}
					delete {support}
					delete {voted::*}
					delete {ctag}
				delete {timer}
		#cancel voting admin
		if arg 1 is "cancel":
			if event-player has permission "player.op":
				if {isvoting} is not set:
					message "§8» §c没有正在进行的投票！"
				else:
					remove bossbar "voting"
					set {_now} to now formatted as "yyyy.MM.dd HH:mm z"
					add "§6%{_now}% §7- §e%player% §7- §6%{name}%§7（%{command}%） §7- §c未通过(被取消)" to {history::*}
					delete {isvoting}
					delete {name}
					delete {timer}
					delete {support}
					delete {voted::*}
					delete {ctag}
					set {cancel} to 1
					broadcast "§8» §e投票已被管理员取消！"
					wait 2 second
					delete {cancel}
			else:
				message "§8» §c你不能这么做！"
		#present voting
		if arg 1 is "list":
			if {command} is not set:
				message "§8» §e正在进行： §6%{name}%"
			else:
				message "§8» §e正在进行： §6%{name}% §7（%{command}%）"
		#vote history
		if arg 1 is "history":
			set {listhistory::*} to reversed {history::*}
			set {_end} to 0
			if arg 2 is not set:
				message "§8» §e投票历史查询： §7(第 1 页)"
				message "§7时间 - 玩家 - 投票名（附加指令） - 是否通过"	
				set {_index} to 1
				loop 10 times:
					if {listhistory::%{_index}%} is not set:
						set {_end} to 1
						message ""
					else:
						message "%{listhistory::%{_index}%}%"
						add 1 to {_index}
				message "§8» §7使用/vote history 2查看下一页"
#The code following is unusable because skript cannot insert expressions in json. 
#				if {_end} is 1:
#					message "§8» §7共计§e%{_index}%§7条"
#					message "§8» §8上一页  |  下一页"
#				else:
#					set {_index} to 11
#					if {listhistory::%{_index}%} is not set:
#						message "§8» §8上一页  |  下一页"
#					else:
#						message "§8» §8上一页  |  <tooltip:§6点击前往下一页><command:/vote history 2>§2§l下一页<reset>"
			else:
				set {_num} to arg 2 parsed as number
				set {_index} to (1 + ({_num} - 1)*10)
				set {_numb} to ({_num} - 2)
				if {listhistory::%{_index}%} is not set:
					message "§8» §c此页没有内容!"
				else:
					message "§8» §e投票历史查询： §7(第 %arg 2%§7 页)"
					message "§7时间 - 玩家 - 投票名（附加指令） - 是否通过"	
					
					loop 10 times:
						if {listhistory::%{_index}%} is not set:
							set {_end} to 1
							message ""
						else:
							message "%{listhistory::%{_index}%}%"
							add 1 to {_index}
					message "§8» §7使用/vote history %({_num} + 1)%§7查看下一页"
#The code following is unusable because skript cannot insert expressions in json. 
#					if {_end} is 1:
#						message "§8» §7共计§e%{_index}%§7条"
#						message "§8» <tooltip:§6点击前往上一页><command:/vote history ({_numb})>§2§l上一页<reset>  §8|  下一页"
#					else:
#						set {_index} to (2 + {_num}*10)
#						if {listhistory::%{_index}%} is not set:
#							message "§8» <tooltip:§6点击前往上一页><command:/vote history ({_numb})>§2§l上一页<reset>  §8|  下一页"
#						else:
#							message "§8» <tooltip:§6点击前往上一页><command:/vote history ({_numb})>§2§l上一页<reset>  §8|  <tooltip:§6点击前往下一页><command:/vote history ({_num})>§2§l下一页<reset>"
